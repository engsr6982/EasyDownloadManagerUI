import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import ".."

Button {
    id: navBtn
    width: parent.width
    height: 36
    checkable: true
    checked: false

    required property ButtonGroup targetGroup // 关联的按钮组
    property string viewSource: "" // 视图源文件
    property string iconText: "" // 文本图标

    ButtonGroup.group: targetGroup

    // 背景层
    background: Rectangle {
        id: bgRect

        anchors.fill: parent
        anchors.leftMargin: 1
        anchors.rightMargin: 1

        radius: Constants.radiusControl
        color: (navBtn.checked || navBtn.hovered) ? "#E2E7E9" : "transparent"

        // 蓝色状态条
        Rectangle {
            id: indicator
            width: 3
            height: navBtn.checked ? 16 : 0
            anchors.left: parent.left
            anchors.leftMargin: 2 // 稍微内缩，防止贴死最左边切角
            anchors.verticalCenter: parent.verticalCenter
            color: "#0078D4"
            radius: 2

            Behavior on height {
                NumberAnimation { duration: 150; easing.type: Easing.OutQuad }
            }
        }
    }

    // 文本层
    contentItem: RowLayout {
        spacing: Constants.itemSpacing
        Layout.fillWidth: true
        Layout.fillHeight: true

        // 图标
        Text {
            // visible: navBtn.iconText !== ""
            text: navBtn.iconText
            font.family: Constants.iconFontFamily
            font.pixelSize: Constants.iconFontSize
            color: navBtn.checked ? Constants.accentPrimary : Constants.textSecondary

            leftPadding: Constants.paddingStandard // 左间距，离状态条远点
            verticalAlignment: Text.AlignVCenter
            horizontalAlignment: Text.AlignHCenter
        }

        // 文本
        Text {
            text: navBtn.text
            font.family: Constants.fontFamily
            font.pixelSize: Constants.fontSizeBody
            color: navBtn.checked ? Constants.textPrimary : Constants.textSecondary

            Layout.fillWidth: true
            verticalAlignment: Text.AlignVCenter
        }
    }
}
